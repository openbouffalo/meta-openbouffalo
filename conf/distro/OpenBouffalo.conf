# Distribution definition for: OpenBouffalo
#

require conf/distro/poky.conf
require conf/distro/include/gcsections.inc

DISTRO = "OpenBouffalo"
DISTRO_NAME="OpenBouffalo"
DISTRO_VERSION="0.0.1"
DISTROOVERRIDES = "poky:poky-tiny"

SDK_VENDOR = "-bflb"

TARGET_VENDOR = "-bflb"

MAINTAINER = "https://github.com/openbouffalo/"

TCLIBC = "glibc"

FULL_OPTIMIZATION="-Os -pipe ${DEBUG_FLAGS}"

POKY_DEFAULT_EXTRA_RRECOMMENDS = ""

# FIXME: what should we do with this?
TCLIBCAPPEND = ""


# Drop native language support. This removes the
# eglibc->bash->gettext->libc-posix-clang-wchar dependency.
USE_NLS="no"

# Comment out any of the lines below to disable them in the build
# DISTRO_FEATURES options:
# alsa bluetooth ext2 pcmcia usbgadget usbhost wifi nfs zeroconf pci
DISTRO_FEATURES_CORE = " acl largefile pam sysvinit multiarch lto"
DISTRO_FEATURES_FS = " largefile smbfs vfat nfs"
DISTRO_FEATURES_NET = " ipv4 ipv6 zeroconf ppp "
DISTRO_FEATURES_USB = "usbhost"
DISTRO_FEATURES_USBGADGET = "usbgadget"
#DISTRO_FEATURES_WIFI = "wifi"

DISTRO_FEATURES = "${DISTRO_FEATURES_CORE} \
                   ${DISTRO_FEATURES_NET} \
                   ${DISTRO_FEATURES_USB} \
                   ${DISTRO_FEATURES_USBGADGET} \
                   ${DISTRO_FEATURES_WIFI} \
                  "

DISTRO_FEATURES:remove = "wayland x11 vulkan opengl"
DISTRO_FEATURES:class-native = "${DISTRO_FEATURES_DEFAULT} ${POKY_DEFAULT_DISTRO_FEATURES}"
DISTRO_FEATURES:class-nativesdk = "${DISTRO_FEATURES_DEFAULT} ${POKY_DEFAULT_DISTRO_FEATURES}"


# enable mdev/busybox for init
INIT_MANAGER ?= "sysvinit"

MACHINE_ESSENTIAL_EXTRA_RDEPENDS = ""

# The mtrace script included by eglibc is a perl script. This means the system
# will build perl in case this package is installed. Since we don't care about
# this script for the purposes of tiny, remove the dependency from here.
RDEPENDS:${PN}-mtrace:pn-eglibc = ""

SKIP_RECIPE[build-appliance-image] = "not buildable with openbouffalo"
SKIP_RECIPE[core-image-rt] = "not buildable with openbouffalo"
SKIP_RECIPE[core-image-rt-sdk] = "not buildable with openbouffalo"
SKIP_RECIPE[core-image-sato] = "not buildable with openbouffalo"
SKIP_RECIPE[core-image-sato-dev] = "not buildable with openbouffalo"
SKIP_RECIPE[core-image-sato-sdk] = "not buildable with openbouffalo"
SKIP_RECIPE[core-image-x11] = "not buildable with openbouffalo"
SKIP_RECIPE[core-image-weston] = "not buildable with openbouffalo"


# Disable python usage in opkg-utils since it won't build with tiny config
PACKAGECONFIG:remove:pn-opkg-utils = "python"

SDK_NAME = "${DISTRO}-${TCLIBC}-${SDKMACHINE}-${IMAGE_BASENAME}-${TUNE_PKGARCH}-${MACHINE}"
SDKPATHINSTALL = "/opt/${DISTRO}/${SDK_VERSION}"

PACKAGE_CLASSES = "package_deb"

DISTRO_FEATURES_BACKFILL_CONSIDERED += "sysvinit"
VIRTUAL-RUNTIME_init_manager = "sysvinit"
PREFERRED_RPROVIDER_ssh = "dropbear"
HOSTTOOLS_NONFATAL:append = " ssh"

PACKAGE_FEED_URIS = "https://openbouffalo.my-ho.st:8443/deploy/openbouffalo/deb/"
PACKAGE_FEED_ARCHS = "all bl808 riscv64"

IMAGE_FSTYPES = "tar.gz wic.xz"
WKS_FILE="sdcard.wks"